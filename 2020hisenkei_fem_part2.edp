load "iovtk"
macro EL(u1,u2) [dx(u1),(dx(u2)+dy(u1)),dy(u2)] //
macro ENL(u1,u2) [(dx(u1)*dx(u1)+dx(u2)*dx(u2))*0.5,(dx(u1)*dy(u1)+dx(u2)*dy(u2)),(dy(u1)*dy(u1)+dy(u2)*dy(u2))*0.5]//
macro dENL(u1,u2,v1,v2) [(dx(u1)*dx(v1)+dx(u2)*dx(v2)),(dx(u1)*dy(v1)+dx(u2)*dy(v2)+dx(v1)*dy(u1)+dx(v2)*dy(u2)),(dy(u1)*dy(v1)+dy(u2)*dy(v2))]//

macro E(u1,u2) (EL(u1,u2)+ENL(u1,u2))//
macro dE(u1,u2,v1,v2) (EL(v1,v2)+dENL(u1,u2,v1,v2))//
macro ddE(u1,u2,v1,v2,w1,w2) dENL(w1,w2,v1,v2)//
macro F2(u1,u2) (E(u1,u2)'*A*E(u1,u2))//
macro dF2(u1,u2,v1,v2) (E(u1,u2)'*A*dE(u1,u2,v1,v2)*2.0)//
macro ddF2(u1,u2,v1,v2,w1,w2) ((dE(u1,u2,v1,v2)'*A*dE(u1,u2,w1,w2))*2.0+(E(u1,u2)'*A*ddE(u1,u2,v1,v2,w1,w2))*2.0)//

real lengthoftate=2.0;
real lengthofyoko=10.0;

border c1(t=1,0){x=0.0;y=lengthoftate*t;label=1;};
border c2(t=0,1){x=lengthofyoko*t;y=0.0;label=2;};
border c3(t=0,1){x=lengthofyoko;y=lengthoftate*t;label=3;};
border c4(t=1,0){x=lengthofyoko*t;y=lengthoftate;label=4;};

int n=20;
int nn=100;
mesh Th = buildmesh(c1(n)+c2(nn)+c3(n)+c4(nn));
plot(Th,wait=true,ps="mesh.eps");

fespace Vh(Th,[P1,P1]);
Vh [u1, u2], [w1, w2], [v1, v2];

real mu=1.0;
real lambda=1.0;
real gairyoku=-0.005;

real a11= 2.0*mu+lambda;
real a22= mu; 
real a33= 2.0*mu+lambda;
real a12= 0.0;
real a13= lambda;
real a23= 0.0;
//symetric part
real a21= a12;
real a31= a13;
real a32= a23;
func A = [ [ a11,a12,a13],[ a21,a22,a23],[ a31,a32,a33] ];  



real errb=0.0;
for(int i=0;i<10;i++){
 solve NonLin([w1,w2],[v1,v2],solver=LU)=
int2d(Th)(ddF2(u1,u2,w1,w2,v1,v2))-int2d(Th)(dF2(u1,u2,v1,v2))
+int1d(Th,3)(gairyoku*v2)
+on(1,w1=0.0,w2=0.0);
;

real res=w1[].linfty;
//errb=max(abs(u1()-),abs(u2()-));
u1[]-=w1[];

mesh th1=movemesh(Th,[x+u1,y+u2]);
plot(th1,ps="heni-nl-elas-part2.eps");

if(res<1e-10) break;
}

//mesh th1 = movemesh(Th,[x+u1, y+u2]);

//sigma_11,sigma_12,sigma_22
//fespace Uh1(th1,[P1,P1,P1]);
//Uh1 [pp11,pp12,pp22];
//[pp11,pp12,pp22]=A*E(u1,u2);

fespace Uh1(Th,[P1,P1,P1]);
Uh1 [pp11,pp12,pp22];
[pp11,pp12,pp22]=A*E(u1,u2);


//plot(pp11, cmm = "stress : P1_11", nbiso = 100,dim=3);
//plot(pp12, cmm = "stress : P1_12", nbiso = 100,dim=3);
//plot(pp22, cmm = "stress : P1_22", nbiso = 100,dim=3);

//plot(th1,wait=1,ps="heni-nl-elas-part2.eps");


int[int] Order = [1,1,1,1];
savevtk("2020hisenkei_fem_part2.vtu",Th,[u1,u2,0],pp11,pp12,pp22,dataname="displacement pp11 pp12 pp22",order=Order);
